apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "cc.aznc.fibspeed"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.main {
        jniLibs.srcDir 'src/main/libs' // change jni lib dir
        jni.srcDirs = [] // disable automatic generate Android.mk
    }
    task ndkBuild(type: Exec) {
        workingDir file('src/main')
        commandLine getNdkBuild()
    }
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
    task ndkBuildClean(type: Exec) {
        workingDir file('src/main')
        commandLine getNdkBuild(), "clean"
    }
    clean.dependsOn 'ndkBuildClean'
    tasks.withType(Exec) {
        environment['NDK_MODULE_PATH'] = getLocalProperty("ndk.module_dir")
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
}
